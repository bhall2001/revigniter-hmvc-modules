<?lc

# PUT HANDLER NAMES INTO THE GLOBAL gControllerHandlers AS A COMMA SEPARATED list-style-type
put "index, chat, addmsg, view, logout" into gControllerHandlers

# THE CONTROLLER HANDLER
command chat
   # LOAD REQUIRED LIBRRIES, MODELS, HELPERS, PLUGINS

   rigLoaderLoadLibrary "Jquery"
   rigLoaderLoadLibrary "Session"

   get rigLoadDatabase()

   rigLoadHelper "asset"

   rigLoadModel "chatmodel"

   put "revIgniter Chat Application Tutorial" into gData["pageTitle"]
end chat

command index
	if rigSessUserdata("name") <> FALSE then
		put rigSessUserdata("name") into gData["user"]
		put FALSE into tLogin
	else
		put rigVarPost("name", TRUE) into tPOSTname
		if tPOSTname <> FALSE then
			rigSetSessUserdata "name", tPOSTname
			put tPOSTname into gData["user"]
			put FALSE into tLogin
		else
			put TRUE into tLogin
		end if
	end if

	if tLogin is FALSE then
		rigJQopen
			rigJQfunctionOpen "loadMessages"
				rigJQvar "previousScroll," & rigJQgetR("'#chatbody'", "attr", "'scrollHeight'")

				put "chat/view" into tAjax["url"]
				put "false" into tAjax["cache"]
				put rigJQsetR("'#chatbody'", "html", "data") & \
				rigJQvarR("currentScroll," & rigJQgetR("'#chatbody'", "attr", "'scrollHeight'")) & \
				rigJQconditionR("currentScroll > previousScroll", rigJQanimateR("'#chatbody'", "scrollTop: currentScroll", "'normal'")) into tAjax["success"]
				rigJQajax tAjax
			rigJQcb

			rigJQeventOpen "'#submitbtn'", "'click'", TRUE
				rigJQxhRequest "post", "chat/addmsg", "userinput:" && rigJQgetR("'#userinput'", "attr", "'value'")
				rigJQset "'#userinput'", "attr", "'value', ''"
			rigJQeventClose

			rigJQeventOpen "'#logout'", "'click'"
				rigJQvar "logout,confirm('Are you sure you want to end the chat session?');"
				rigJQcondition "logout==true", "window.location = 'logout';"
			rigJQeventClose

			rigJQaddStatement "setInterval (loadMessages, 2500);"

		rigJQclose
		put the result into gData["JQscript"]

		put getMsgData() into gData["msgList"]

	end if

	rigSetHeader "Cache-Control: no-cache"

	get rigLoadView("chatheader")
	if tLogin is TRUE then
		get rigLoadView("chatlogin")
	else
		get rigLoadView("chatcontent")
	end if
	get rigLoadView("chatfooter")
end index

command addmsg
   put rigSessUserdata("name") into tName
   put rigVarPost("userinput") into tMsg

   if (tMsg <> FALSE) and (tMsg <> "") then
      _addMsg tName, tMsg
   end if

   index
end addmsg

command view
   put getMsgData() into gData["msgList"]
   get rigLoadView("chatlist")
end view

command logout
   put rigSessUserdata("name") into tName
   if tName <> FALSE then
      put "User" && tName && "has left the chat session." into tMsg
      _addMsg tName, tMsg
   end if

   rigSessDestroy

   get rigLoadView("chatheader")
   get rigLoadView("chatlogin")
   get rigLoadView("chatfooter")
end logout


--| END OF chat.lc
--| Location: ./system/application/controllers/chat.lc
